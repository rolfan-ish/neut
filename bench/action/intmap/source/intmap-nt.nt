import {
  core.control {grow},
  core.environment {get-argv},
  core.int.io {print-int-line},
  core.int.parse {parse-int},
  core.intmap {empty, insert, intmap, lookup},
  core.random {random-int},
}

define make-big-dict(size: int): intmap(int) {
  grow(empty, size, function (acc, _) {
    let key = random-int(1000000);
    let val = random-int(1000000);
    insert(acc, key, val)
  })
}

define random-lookup-sum(count: int, d: &intmap(int)): unit {
  let rsum =
    grow(0, count, function (acc, _) {
      let key = random-int(1000000);
      let val on key =
        match lookup(d, *key) {
        | Left(_) =>
          0
        | Right(v) =>
          *v
        };
      let _ = key;
      add-int(acc, val)
    });
  print-int-line(rsum);
}

define main(): unit {
  let args = get-argv();
  match args {
  | Cons(_, Cons(size-str, Nil)) =>
    pin size-str = size-str;
    match parse-int(size-str) {
    | Right(size) =>
      pin big-dict = make-big-dict(size);
      random-lookup-sum(size, big-dict);
    | Left(_) =>
      print("couldn't parse `");
      print(size-str);
      print("` as an integer\n");
    }
  | _ =>
    print("usage: intmap-nt SIZE\n")
  }
}
